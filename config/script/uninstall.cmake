set(PROJECT_BINARY_DIR "${CMAKE_ARGV4}")
set(DEV_PRETEND False)

set(DEV_PROTECTED)
if (WIN32)
    set(DEV_PROTECTED                  "^.:[/\\]Program Files$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^.:[/\\]Program Files \(x86\)$")
else()
    set(DEV_PROTECTED                  "^/usr/(local/)?[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/icons/[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/icons/hicolor/[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/icons/hicolor/[^/]*/apps$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/man/[^/]*$")
endif()

# Removing files
file(STRINGS "${PROJECT_BINARY_DIR}/install_manifest.txt" DEV_FILEPATHS)
list(SORT DEV_FILEPATHS ORDER DESCENDING)
foreach (DEV_FILEPATH IN LISTS DEV_FILEPATHS)
    message("-- Uninstalling: ${DEV_FILEPATH}")
    get_filename_component(DEV_DIRPATH "${DEV_FILEPATH}" DIRECTORY)
    list(APPEND DEV_DIRPATHS "${DEV_DIRPATH}")
    get_filename_component(DEV_DIRPATH "${DEV_DIRPATH}" DIRECTORY)
    list(APPEND DEV_DIRPATHS "${DEV_DIRPATH}")
    if (NOT DEV_PRETEND)
        file(REMOVE "${DEV_FILEPATH}")
        if (EXISTS "${DEV_FILEPATH}")
            message(WARNING "Could not remove ${DEV_FILEPATH}")
        endif()
    endif()
endforeach()
list(SORT DEV_DIRPATHS ORDER DESCENDING)
list(REMOVE_DUPLICATES DEV_DIRPATHS)

# Removing directories
cmake_policy(SET CMP0057 NEW)
foreach (DEV_DIRPATH IN LISTS DEV_DIRPATHS)
    if ("${DEV_DIRPATH}" MATCHES "${DEV_PROTECTED}")
        continue()
    endif()

    file(GLOB DEV_ENTRIES LIST_DIRECTORIES True "${DEV_DIRPATH}/*")
    set (DEV_DELETE True)
    foreach (DEV_ENTRY IN LISTS DEV_ENTRIES)
        if ((NOT "${DEV_ENTRY}" IN_LIST DEV_DIRPATHS) AND (NOT "${DEV_ENTRY}" IN_LIST DEV_FILEPATHS))
            set (DEV_DELETE False)
            break()
        endif()
    endforeach()
    if (DEV_DELETE)
        message("-- Uninstalling: ${DEV_DIRPATH}")
        if (NOT DEV_PRETEND)
            file(REMOVE_RECURSE "${DEV_DIRPATH}")
            if (EXISTS "${DEV_DIRPATH}")
                message(WARNING "Could not remove ${DEV_DIRPATH}")
            endif()
        endif()
    endif()
endforeach()
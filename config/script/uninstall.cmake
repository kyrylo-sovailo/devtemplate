set(PROJECT_BINARY_DIR "${CMAKE_ARGV3}")
set(DEV_PRETEND False)

set(DEV_PROTECTED)
if (WIN32)
    set(DEV_PROTECTED                  "^.:[/\\]Program Files$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^.:[/\\]Program Files \(x86\)$")
else()
    set(DEV_PROTECTED                  "^/usr/(local/)?[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/icons/[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/icons/hicolor/[^/]*$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/icons/hicolor/[^/]*/apps$")
    set(DEV_PROTECTED "${DEV_PROTECTED}|^/usr/(local/)?share/man/[^/]*$")
endif()

# Removing files
file(STRINGS "${PROJECT_BINARY_DIR}/install_manifest.txt" DEV_FILEPATHS)
list(SORT DEV_FILEPATHS ORDER DESCENDING)
foreach (DEV_FILEPATH ${DEV_FILEPATHS})
    message("-- Uninstalling: ${DEV_FILEPATH}")
    get_filename_component(DEV_DIRPATH "${DEV_FILEPATH}" DIRECTORY)
    list(APPEND DEV_DIRPATHS "${DEV_DIRPATH}")
    get_filename_component(DEV_DIRPATH "${DEV_DIRPATH}" DIRECTORY)
    list(APPEND DEV_DIRPATHS "${DEV_DIRPATH}")
    if (NOT DEV_PRETEND)
        file(REMOVE "${DEV_FILEPATH}")
        if (EXISTS "${DEV_FILEPATH}")
            message(WARNING "Could not remove ${DEV_FILEPATH}")
        endif()
    endif()
endforeach()

# Removing directories
list(SORT DEV_DIRPATHS ORDER DESCENDING)
list(REMOVE_DUPLICATES DEV_DIRPATHS)
foreach (DEV_DIRPATH ${DEV_DIRPATHS})
    if (NOT "${DEV_DIRPATH}" MATCHES "${DEV_PROTECTED}")
        file(GLOB DEV_ENTRIES LIST_DIRECTORIES True "${DEV_DIRPATH}/*")
        set (DEV_DELETE True)
        foreach (DEV_ENTRY ${DEV_ENTRIES})
            list(FIND DEV_DIRPATHS "${DEV_ENTRY}" DEV_INDEX)
            if (${DEV_INDEX} EQUAL -1)
                list(FIND DEV_FILEPATHS "${DEV_ENTRY}" DEV_INDEX)
                if (${DEV_INDEX} EQUAL -1)
                    set (DEV_DELETE False)
                    break()
                endif()
            endif()
        endforeach()
        if (DEV_DELETE)
            message("-- Uninstalling: ${DEV_DIRPATH}")
            if (NOT DEV_PRETEND)
                file(REMOVE_RECURSE "${DEV_DIRPATH}")
                if (EXISTS "${DEV_DIRPATH}")
                    message(WARNING "Could not remove ${DEV_DIRPATH}")
                endif()
            endif()
        endif()
    endif()
endforeach()
file(STRINGS "install_manifest.txt" MANIFEST_FILE)

set(MANIFEST "${MANIFEST_FILE}")
while(MANIFEST)
    list(POP_FRONT MANIFEST FILEPATH)
    if(EXISTS "${FILEPATH}")
        message("-- Uninstalling: ${FILEPATH}")
        file(REMOVE "${FILEPATH}")
        if (EXISTS "${FILEPATH}")
            message(FATAL_ERROR "Could not delete ${FILEPATH}")
        endif()
    endif()
endwhile()

set(MANIFEST "${MANIFEST_FILE}")
while(MANIFEST)
    list(POP_FRONT MANIFEST FILEPATH)
    get_filename_component(DIRPATH "${FILEPATH}" DIRECTORY)
    get_filename_component(DIRNAME "${DIRPATH}" NAME)
    if("${DIRNAME}" STREQUAL "${CMAKE_ARGV3}"
    AND NOT ("${DIRNAME}" STREQUAL "include")
    AND NOT ("${DIRNAME}" STREQUAL "bin")
    AND NOT ("${DIRNAME}" STREQUAL "lib")
    AND NOT ("${DIRNAME}" STREQUAL "lib32")
    AND NOT ("${DIRNAME}" STREQUAL "lib64"))
        if(EXISTS "${DIRPATH}")
            message("-- Uninstalling: ${DIRPATH}")
            file(REMOVE_RECURSE "${DIRPATH}")
            if (EXISTS "${DIRPATH}")
                message(FATAL_ERROR "Could not delete ${DIRPATH}")
            endif()
        endif()
    endif()
endwhile()